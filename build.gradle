group 'com.dounine'
version '1.0.4'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.2"
    }
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'signing'
apply plugin: 'scala'
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'spark-sql-datasource'
                description = 'spark sql datasource'
                url = 'https://github.com/dounine/spark-sql-datasource'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'lake'
                        name = 'lake'
                        email = 'amwoqmgo@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/dounine/spark-sql-datasource.git'
                    developerConnection = 'scm:git:ssh://github.com/dounine/spark-sql-datasource.git'
                    url = 'https://github.com/dounine/scala-filebeat'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username NEXUS_USERNAME
                password NEXUS_PASSWORD
            }
        }
    }
}

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.hortonworks.com/content/repositories/releases/" }
}


task copyJars(type: Copy) {
    from configurations.runtime
    into new File('build/libs/lib')
}
compileJava.dependsOn copyJars

shadowJar {
    zip64 true
}

signing {
    sign publishing.publications.mavenJava
}

ext {
    flinkVersion = "1.8-SNAPSHOT"
    hbaseVersion = "2.0.0"
    slf4jVersion = "1.7.25"
}

dependencies {
    compile 'org.scala-lang:scala-library:2.12.12'

    compile group: 'org.apache.spark', name: 'spark-sql_2.12', version: '3.0.0'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.47'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}
